plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.trigonated.ctwtopheadlines"
        minSdk 28
        targetSdk 31
        versionCode 1
        versionName "1.0"

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.trigonated.ctwtopheadlines.misc.CustomTestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments["dagger.hilt.disableModulesHaveInstallInCheck"] = "true"
            }
        }

        buildConfigField("String", "NEWSAPI_BASE_URL", "\"https://newsapi.org/v2/\"")
        buildConfigField("String", "NEWSAPI_API_KEY", "\"API_KEY\"")
    }

    flavorDimensions 'source'
    productFlavors {
        bbcnews {
            dimension 'source'
            applicationIdSuffix ".bbcnews"
            versionNameSuffix "-bbcnews"
            buildConfigField("String", "NEWSAPI_SOURCES", "\"bbc-news\"")
        }

        arstechnica {
            dimension 'source'
            applicationIdSuffix ".arstechnica"
            versionNameSuffix "-arstechnica"
            buildConfigField("String", "NEWSAPI_SOURCES", "\"ars-technica\"")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
    sourceSets {
        arstechnica {
            assets {
                srcDirs 'src\\arstechnica\\assets', 'src\\assets', 'src\\main\\assets'
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.1"
    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    // Biometric authentication
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha04"
    // Dagger Hilt
    implementation 'com.google.dagger:hilt-android:2.37'
    kapt "com.google.dagger:hilt-compiler:2.37"
    kapt "com.google.dagger:hilt-android-compiler:2.37"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    // GSON
    implementation "com.google.code.gson:gson:2.8.6"
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    testImplementation "org.mockito:mockito-core:4.3.1"
    androidTestImplementation "org.mockito:mockito-core:4.3.1"
    androidTestImplementation "org.mockito:mockito-android:4.3.1"
    testImplementation 'app.cash.turbine:turbine:0.7.0'
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.37"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:2.37"
}